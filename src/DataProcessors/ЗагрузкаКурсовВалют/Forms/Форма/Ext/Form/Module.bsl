
&НаКлиенте
Процедура ЗагрузитьКурсы(Команда)
	
	ДанныеОКурсах = ПолучитьДанныеОКурсахССайта();
	ЗагрузитьДанныеВРегистр(ДанныеОКурсах);
	ПоказатьОповещениеПользователя("Курсы валют загружены");
	
КонецПроцедуры

&НаКлиенте
Функция ПолучитьДанныеОКурсахССайта()
	
	// https://www.cbr-xml-daily.ru/daily_utf8.xml
	
	Соединение = Новый HTTPСоединение("www.cbr-xml-daily.ru",,,,,, Новый ЗащищенноеСоединениеOpenSSL);
	Запрос = Новый HTTPЗапрос("/daily_utf8.xml");
	Ответ = Соединение.Получить(Запрос);
	
	Если Ответ.КодСостояния <> 200 Тогда
		ВызватьИсключение "Не удалось соедениться с https://www.cbr-xml-daily.ru/daily_utf8.xml";
	КонецЕсли;	
	
	Данные = Ответ.ПолучитьТелоКакСтроку();
	
	Возврат Данные;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьДанныеВРегистр(ДанныеОКурсах)
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ДанныеОКурсах);
	
	ПостроительDOM = Новый ПостроительDOM;
	Документ = ПостроительDOM.Прочитать(ЧтениеXML);
	
	ДанныеОВалютах = Документ.ЭлементДокумента;
	
	Для Каждого ДанныеОВалюте Из ДанныеОВалютах.ДочерниеУзлы Цикл
		
		КодВалюты = "";
		КратностьВалюты = "";
		КурсВалюты = "";
		
		Для Каждого СвойствоВалюты Из ДанныеОВалюте.ДочерниеУзлы Цикл
			
			Если СвойствоВалюты.ИмяУзла = "CharCode" Тогда
				КодВалюты = СвойствоВалюты.ТекстовоеСодержимое;
			КонецЕсли;
			
			Если СвойствоВалюты.ИмяУзла = "Nominal" Тогда
				КратностьВалюты = Число(СвойствоВалюты.ТекстовоеСодержимое);
			КонецЕсли;
			
			Если СвойствоВалюты.ИмяУзла = "Value" Тогда
				КурсВалюты = Число(СвойствоВалюты.ТекстовоеСодержимое);
			КонецЕсли;
			
		КонецЦикла;
		
		ВалютаСсылка = Справочники.Валюты.НайтиПоКоду(КодВалюты);
		
		Если ЗначениеЗаполнено(ВалютаСсылка) Тогда
			
			Запись = РегистрыСведений.КурсыВалют.СоздатьМенеджерЗаписи();
			Запись.Валюта = ВалютаСсылка;
			Запись.Кратность = КратностьВалюты;
			Запись.Курс = КурсВалюты;
			Запись.Период = ТекущаяДата();
			Запись.Записать();			
			
		КонецЕсли;			
		
	КонецЦикла;
	
	ЧтениеXML.Закрыть();	
	
КонецПроцедуры
