
Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если РазделитьПоТоварам Тогда
		Сумма = РасшифровкаПокупки.Итог("Сумма");
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, Режим)
	
	СуммаВРублях = РегистрыСведений.КурсыВалют.СуммаВРублях(Сумма, Счет.Валюта, Дата);	

	// регистр ЛичныеСчета Расход
	Движения.ЛичныеСчета.Записывать = Истина;
	Движение = Движения.ЛичныеСчета.Добавить();
	Движение.ВидДвижения = ВидДвиженияНакопления.Расход;
	Движение.Период = Дата;
	Движение.Валюта = Счет.Валюта;
	Движение.Счет = Счет;
	Движение.Сумма = СуммаВРублях;
	Движение.СуммаВалютная = Сумма;

	// регистр Расходы 
	Движения.Расходы.Записывать = Истина;
	
	Если РазделитьПоТоварам Тогда
		
		Для Каждого ТекСтрокаРасшифровкаПокупки Из РасшифровкаПокупки Цикл
			
			СуммаВРубляхДляСтроки = РегистрыСведений.КурсыВалют.СуммаВРублях(ТекСтрокаРасшифровкаПокупки.Сумма, Счет.Валюта, Дата);
			
			Движение = Движения.Расходы.Добавить();
			Движение.Период = Дата;
			Движение.СтатьяДвижений = СтатьяДвижений;
			Движение.Номенклатура = ТекСтрокаРасшифровкаПокупки.Номенклатура;
			Движение.Валюта = Счет.Валюта;
			Движение.Счет = Счет;
			Движение.Сумма = СуммаВРубляхДляСтроки;
			Движение.СуммаВалютная = ТекСтрокаРасшифровкаПокупки.Сумма;
		КонецЦикла;
		
	Иначе
		
		Движение = Движения.Расходы.Добавить();
		Движение.Период = Дата;
		Движение.СтатьяДвижений = СтатьяДвижений;
		Движение.Валюта = Счет.Валюта;
		Движение.Счет = Счет;
		Движение.Сумма = СуммаВРублях;
		Движение.СуммаВалютная = Сумма;
	
	КонецЕсли;

КонецПроцедуры
