
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ОбновитьДиаграммыНаСервере();
		
КонецПроцедуры

&НаСервере
Процедура ОбновитьДиаграммыНаСервере()

	ОбновитьДиаграммуОстаткиПоЛичнымСчетам();
	ОбновитьДиаграммуДоходы();
	ОбновитьДиаграммуРасходы();
	ОбновитьДиаграммуТопПокупок();

КонецПроцедуры

&НаСервере
Процедура ОбновитьДиаграммуОстаткиПоЛичнымСчетам()

	Диаграмма = ОстаткиПоСчетам;
	Диаграмма.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ЛичныеСчетаОстатки.Счет КАК Счет,
	|	ЛичныеСчетаОстатки.СуммаОстаток КАК СуммаОстаток
	|ИЗ
	|	РегистрНакопления.ЛичныеСчета.Остатки КАК ЛичныеСчетаОстатки";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Точка = Диаграмма.Точки.Добавить("Сумма в рублях");
	
	Пока Выборка.Следующий() Цикл
		
		Серия = Диаграмма.УстановитьСерию(Выборка.Счет);
		Диаграмма.УстановитьЗначение(Точка, Серия, Выборка.СуммаОстаток);
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьДиаграммуДоходы()

	Диаграмма = Доходы;
	Диаграмма.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ДоходыОбороты.Период КАК Период,
	|	ДоходыОбороты.СтатьяДвижений КАК СтатьяДвижений,
	|	ДоходыОбороты.СуммаОборот КАК СуммаОборот
	|ИЗ
	|	РегистрНакопления.Доходы.Обороты(, , Месяц, ) КАК ДоходыОбороты
	|УПОРЯДОЧИТЬ ПО Период ВОЗР";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	
	Пока Выборка.Следующий() Цикл
		
		ПредставлениеПериода = Формат(Выборка.Период,"ДФ=ММММ.гггг"); 
		
		Точка = Диаграмма.УстановитьТочку(ПредставлениеПериода);
		Серия = Диаграмма.УстановитьСерию(Выборка.СтатьяДвижений);
		
		Диаграмма.УстановитьЗначение(Точка, Серия, Выборка.СуммаОборот);
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьДиаграммуРасходы()

	Диаграмма = Расходы;
	Диаграмма.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходыОбороты.Период КАК Период,
	|	РасходыОбороты.СтатьяДвижений КАК СтатьяДвижений,
	|	РасходыОбороты.СуммаОборот КАК СуммаОборот
	|ИЗ
	|	РегистрНакопления.Расходы.Обороты(, , Месяц, ) КАК РасходыОбороты
	|УПОРЯДОЧИТЬ ПО Период ВОЗР";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	
	Пока Выборка.Следующий() Цикл
		
		ПредставлениеПериода = Формат(Выборка.Период,"ДФ=ММММ.гггг"); 
		
		Точка = Диаграмма.УстановитьТочку(ПредставлениеПериода);
		Серия = Диаграмма.УстановитьСерию(Выборка.СтатьяДвижений);
		
		Диаграмма.УстановитьЗначение(Точка, Серия, Выборка.СуммаОборот);
		
	КонецЦикла;

КонецПроцедуры

&НаСервере
Процедура ОбновитьДиаграммуТопПокупок()

	Диаграмма = ТопПокупок;
	Диаграмма.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	РасходыОбороты.Номенклатура КАК Номенклатура,
	|	РасходыОбороты.СуммаОборот КАК СуммаОборот
	|ИЗ
	|	РегистрНакопления.Расходы.Обороты(&ДатаНачала, , , Номенклатура <> ЗНАЧЕНИЕ(Справочник.Номенклатура.ПустаяСсылка)) КАК РасходыОбороты
	|
	|УПОРЯДОЧИТЬ ПО
	|	СуммаОборот УБЫВ";
	Запрос.УстановитьПараметр("ДатаНачала", НачалоМесяца(ТекущаяДата()));
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Серия = Диаграмма.Серии.Добавить("Сумма в рублях");
	
	Счетчик = 1;
	
	Пока Выборка.Следующий() Цикл
		
		Если Счетчик > 20 Тогда
			Прервать;
		КонецЕсли;
		
		Точка = Диаграмма.УстановитьТочку(Выборка.Номенклатура);		
		Диаграмма.УстановитьЗначение(Точка, Серия, Выборка.СуммаОборот);
		
		Счетчик = Счетчик + 1; 
		
	КонецЦикла;
	
КонецПроцедуры


&НаКлиенте
Процедура ОбновитьДиаграммыКоманда(Команда)
	
	ОбновитьДиаграммыНаСервере();
	
КонецПроцедуры


&НаКлиенте
Процедура ОбновитьДиаграммыНаКлиенте()

	ОбновитьДиаграммыНаСервере();	
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновлятьАвтоматическиПриИзменении(Элемент)
	
	Если ОбновлятьАвтоматически Тогда
		Элементы.ИнтервалОбновленияДиаграмм.Доступность = Истина;
		ПодключитьОбработчикОжидания("ОбновитьДиаграммыНаКлиенте", ИнтервалОбновленияДиаграмм);
	Иначе
		Элементы.ИнтервалОбновленияДиаграмм.Доступность = Ложь;
		ОтключитьОбработчикОжидания("ОбновитьДиаграммыНаКлиенте");
	КонецЕсли;
	
КонецПроцедуры

ИнтервалОбновленияДиаграмм = 5;
